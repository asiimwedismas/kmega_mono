// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_version = '1.2.0-rc02'
        dagger_hilt_version = '2.40.1'
        kotlin_version = '1.7.10'
        coroutines_version = '1.6.0'
        lifecycle_version = '2.4.1'
        room_version = '2.4.1'
        nav_version = '2.4.2'
        fragment_version = '1.4.1'
        hilt_version = '1.0.0'
        work_version = '2.7.1'
        accompanist_version = '0.19.0'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "com.google.gms:google-services:4.3.10"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_hilt_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdk 32

                defaultConfig {
                    minSdk 26
                    targetSdk 32
                    versionCode 1
                    versionName "1.0"

                    testInstrumentationRunner "me.asiimwedismas.kmega_mono.CustomHiltTestRunner"
                    vectorDrawables {
                        useSupportLibrary true
                    }
                }

                buildTypes {
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }
                buildFeatures {
                    compose true
                }
                composeOptions {
                    kotlinCompilerExtensionVersion compose_version
                }
                packagingOptions {
                    resources {
                        excludes += '/META-INF/{AL2.0,LGPL2.1}'
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
